
#Область ПрограммныйИнтерфейс

Процедура ПередЗакрытиемФормыНастроек(Форма, Отказ) Экспорт
	
	Если Форма.Модифицированность И НЕ Форма.ОповеститьОбИзмененииЗначенийНастроек Тогда
		
		Отказ = Истина;
		
		ДополнительныеПараметры = Новый Структура("Форма", Форма);
		ОписаниеОповещения = Новый ОписаниеОповещения("ОповещениеПередЗакрытием", ЭтотОбъект, ДополнительныеПараметры);
		
		ТекстВопроса = НСтр("ru = 'Данные были изменены. Сохранить изменения?'");
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередНачаломРаботыСистемы(Отказ) Экспорт
	
	ПервыйЗапуск = ОбщегоНазначенияВызовСервера.ОбработатьЗапускПриложения();
	
	Если ПервыйЗапуск Тогда
		
		// ПодключаемоеОборудование
		МенеджерОборудованияВызовСервераПереопределяемый.ОбновлениеБиблиотеки();
		// Конец ПодключаемоеОборудование
		
		РезервноеКопированиеКлиент.УстановитьПутьРезервногоКопированияПриПервомЗапуске();
		
		ОбщегоНазначенияВызовСервера.УстановитьРежимСинхронизацииДемоверсии();
		
		ПараметрыФормы = Новый Структура("ПервыйЗапуск", ПервыйЗапуск);
		ОткрытьФорму("Обработка.НастройкиПриложения.Форма.РежимСинхронизации", ПараметрыФормы,,,,,,РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
		
	КонецЕсли;
	
	ОбщегоНазначенияВызовСервера.ОбновитьПриложение();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОповещениеПередЗакрытием(Результат, ДополнительныеПараметры) Экспорт
	
	Форма = ДополнительныеПараметры.Форма;
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		
		Форма.СохранитьНаКлиенте();
		
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		
		Форма.Модифицированность = Ложь;
		Форма.Закрыть();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
