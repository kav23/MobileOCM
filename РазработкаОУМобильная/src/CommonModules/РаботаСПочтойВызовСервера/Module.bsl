
#Область ПрограммныйИнтерфейс

Функция ПолучателиОтчетовПоПочте() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПолучателиОтчетовПоПочте.АдресЭлектроннойПочты КАК АдресЭлектроннойПочты
	|ИЗ
	|	РегистрСведений.ПолучателиОтчетовПоПочте КАК ПолучателиОтчетовПоПочте";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Получатели = РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("АдресЭлектроннойПочты");
	
	Возврат Получатели;
	
КонецФункции

Функция СформироватьОбращениеВПоддержку() Экспорт
	
	АдресПочтыПоддержки = EmailТехническойПоддержки();
	
	ПараметрыПисьма = "mailto:%АдресПочтыПоддержки%?";
	ПараметрыПисьма = СтрЗаменить(ПараметрыПисьма, "%АдресПочтыПоддержки%", АдресПочтыПоддержки);
	ТекстЗаголовокПисьма = НСтр("ru = '%ИмяКонфигурации%. %ТемаОбращения%'");
	ИмяКонфигурации = Метаданные.Синоним;
	ТекстЗаголовокПисьма = СтрЗаменить(ТекстЗаголовокПисьма, "%ИмяКонфигурации%", ИмяКонфигурации);
	ТекстТемаОбращения = "";
	ТекстОбращенияВПоддержку = "";
	ТекстРазделитель = "---";
	ТекстЗаголовокПисьма = СтрЗаменить(ТекстЗаголовокПисьма, "%ТемаОбращения%", ТекстТемаОбращения);
	ЗаголовокПисьма = "subject=" + Кодировать(ТекстЗаголовокПисьма);
	ТекстОбращенияВПоддержку = ТекстОбращенияВПоддержку + Символы.ПС
		+ СформироватьСлужебноеСообщение();
	ПараметрыПисьма = ПараметрыПисьма + ЗаголовокПисьма + "&body=" + Кодировать(ТекстОбращенияВПоддержку);
	Возврат ПараметрыПисьма;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция EmailТехническойПоддержки()
	
	Возврат "mobkas@1c.ru";
	
КонецФункции

Функция СформироватьСлужебноеСообщение()
	
	ТекстСлужебнойИнформации = НСтр("ru = 'Техническая информация Вашего мобильного устройства:
	|---'");
	
	СисИнфо = Новый СистемнаяИнформация;
	ТекстСлужебнойИнформации = ТекстСлужебнойИнформации + Символы.ПС
		+ НСтр("ru = 'Версия ОС:'") + " "+ СокрЛП(СисИнфо.ВерсияОС);
	ТекстСлужебнойИнформации = ТекстСлужебнойИнформации + Символы.ПС
		+ НСтр("ru = 'Оперативная память:'") + " " + СокрЛП(СисИнфо.ОперативнаяПамять);
	ТекстСлужебнойИнформации = ТекстСлужебнойИнформации + Символы.ПС
		+ НСтр("ru = 'Процессор:'") + " " + СокрЛП(СисИнфо.Процессор);
		
	
	ТекстСлужебнойИнформации = ТекстСлужебнойИнформации + Символы.ПС
		+ НСтр("ru = 'Версия платформы 1С:'") + " " + СокрЛП(СисИнфо.ВерсияПриложения);
	ТекстСлужебнойИнформации = ТекстСлужебнойИнформации + Символы.ПС
		+ НСтр("ru = 'Версия приложения:'") + " " + СокрЛП(Метаданные.Версия);
	Возврат ТекстСлужебнойИнформации;
	
КонецФункции

Функция Кодировать(Текст)
	
	Возврат КодироватьСтроку(Текст, СпособКодированияСтроки.КодировкаURL);
	
КонецФункции

#КонецОбласти
