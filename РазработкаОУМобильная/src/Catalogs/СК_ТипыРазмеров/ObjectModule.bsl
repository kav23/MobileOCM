

Функция ЕстьПовторяющиесяЗаписи()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	СК_ТипыРазмеров.Ссылка
	               |ИЗ
	               |	Справочник.СК_ТипыРазмеров КАК СК_ТипыРазмеров
	               |ГДЕ
	               |	СК_ТипыРазмеров.ГруппаПродукции = &ГруппаПродукции
	               |	И СК_ТипыРазмеров.ФормаСечения = &ФормаСечения
	               |	И СК_ТипыРазмеров.Мерность = &Мерность
	               |	И СК_ТипыРазмеров.Ссылка <> &Ссылка
	               |	И (НЕ СК_ТипыРазмеров.ПометкаУдаления)";
					   
	Запрос.УстановитьПараметр("ГруппаПродукции",ГруппаПродукции);
    Запрос.УстановитьПараметр("ФормаСечения",ФормаСечения);
	Запрос.УстановитьПараметр("Мерность",Мерность);
    Запрос.УстановитьПараметр("Ссылка",Ссылка);

	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Ложь;
	ИначеЕсли Стандарты.Количество() > 0 Тогда 
		// Проверим есть ли типы размеров с такими стандартами
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	СК_ТипыРазмеровСтандарты.Ссылка КАК ТипРазмера,
		|	СК_ТипыРазмеровСтандарты.Стандарт КАК Стандарт
		|ИЗ
		|	Справочник.СК_ТипыРазмеров.Стандарты КАК СК_ТипыРазмеровСтандарты
		|ГДЕ
		|	СК_ТипыРазмеровСтандарты.Ссылка В(&СписокТиповРазмера)
		|	И СК_ТипыРазмеровСтандарты.Стандарт В(&СписокСтандартов)";
		
		Запрос.УстановитьПараметр("СписокТиповРазмера", РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Ссылка"));
		Запрос.УстановитьПараметр("СписокСтандартов", Стандарты.ВыгрузитьКолонку("Стандарт"));
		
		Результат = Запрос.Выполнить();
		Выборка = Результат.Выбрать();
		Флаг = Ложь;
		
		Пока Выборка.Следующий() Цикл
			Флаг = Истина;
			Сообщить("Стандарт " + Выборка.Стандарт + " указан в " + Выборка.ТипРазмера);
		КонецЦикла;
		Возврат Флаг;
		 
	Иначе
		Сообщить("Есть типы размеров с аналогичными параметрами.");
		Возврат Истина
	КонецЕсли; 
	
КонецФункции

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка = истина Тогда
		Возврат;
	КонецЕсли; 
	
	//проверка на ввод повторяюейся записи
	Если ЕстьПовторяющиесяЗаписи() Тогда
		//есть аналогичная запись
		Отказ = Истина;
	КонецЕсли;
		
	//Проверка на ввод пустой строки
	Если ГруппаПродукции.Пустая() Тогда
		Сообщить("Не заполнена группа продукции.");
		Отказ=Истина;
	КонецЕсли;

КонецПроцедуры

