
#Область ОбработчикиСобытийФорм

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗапретИзмененияДрайвера = Объект.Ссылка <> Справочники.ПодключаемоеОборудование.ПустаяСсылка();
	// Защита от изменения типа устройства если тип явно задан или экземпляр уже создан.
	Элементы.ТипОборудования.ТолькоПросмотр = ЗапретИзмененияДрайвера;
	// Защита от изменения обработчика драйвера уже созданного экземпляра устройства.
	Элементы.ДрайверОборудования.ТолькоПросмотр = ЗапретИзмененияДрайвера;
	Элементы.ДрайверОборудования.КнопкаОткрытия = Не ЗапретИзмененияДрайвера;
	
	// Загрузка и установка списка доступных обработок.
	СписокДрайверов = ПолучитьСписокДрайверов(Объект.ТипОборудования, НЕ ЗапретИзмененияДрайвера);
	Элементы.ДрайверОборудования.СписокВыбора.Очистить();
	Для каждого СтрокаСписка Из СписокДрайверов Цикл
		Элементы.ДрайверОборудования.СписокВыбора.Добавить(СтрокаСписка.Значение, СтрокаСписка.Представление);
	КонецЦикла;
	
	Элементы.УстановитьДрайвер.Видимость = НЕ ПустаяСтрока(Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если НЕ ПроверкаУникальностиПоДрайверу() Тогда
		Отказ = Истина;
		Текст = НСтр("ru='Экземпляр подключаемого оборудования для данного драйвера уже создан.'");
		Сообщить(Текст);
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Настроить(Команда)
	
	Записать();
	
	Если МенеджерОборудованияКлиент.ВыполнитьНастройкуОборудования(Объект.Ссылка) Тогда
		ЭтаФорма.Прочитать(); 
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементов

&НаКлиенте
Процедура ТипОборудованияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)

	Если Объект.ТипОборудования <> ВыбранноеЗначение Тогда
		Объект.ТипОборудования = ВыбранноеЗначение;
		
		// Загрузка и установка списка доступных обработок
		СписокДрайверов = ПолучитьСписокДрайверов(Объект.ТипОборудования, НЕ ЗапретИзмененияДрайвера);
		Элементы.ДрайверОборудования.СписокВыбора.Очистить();
		Для каждого СтрокаСписка Из СписокДрайверов Цикл
			Элементы.ДрайверОборудования.СписокВыбора.Добавить(СтрокаСписка.Значение, СтрокаСписка.Представление);
		КонецЦикла;
		
		Объект.ДрайверОборудования = ПредопределенноеЗначение("Справочник.ДрайверыОборудования.ПустаяСсылка");
		Объект.Наименование = "";
		
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДрайверОборудованияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ВыбранноеЗначение <> Объект.ДрайверОборудования Тогда
		ОбработатьВыборОбработчика(ВыбранноеЗначение, СтандартнаяОбработка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьВыборОбработчика(ВыбранныйОбработчик, СтандартнаяОбработка = Истина)
	
	Объект.Наименование = НСтр("ru='Оборудование'") + " '" + Строка(ВыбранныйОбработчик) + "'";
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПолучитьСписокДрайверов(ТипОборудования, ТолькоДоступные)
	
	СписокДрайверов = Новый СписокЗначений();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДрайверыОборудования.Ссылка,
		|	ДрайверыОборудования.Наименование,
		|	ДрайверыОборудования.ТипОборудования
		|ИЗ
		|	Справочник.ДрайверыОборудования КАК ДрайверыОборудования
		|ГДЕ
		|	(ДрайверыОборудования.ТипОборудования = &ТипОборудования)
		|	%УСЛОВИЕ%
		|УПОРЯДОЧИТЬ ПО ДрайверыОборудования.Наименование";
		
	Если ТолькоДоступные Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "%УСЛОВИЕ%", "И НЕ ДрайверыОборудования.ПометкаУдаления");
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "%УСЛОВИЕ%", "");
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ТипОборудования", ТипОборудования);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		СписокДрайверов.Добавить(ВыборкаДетальныеЗаписи.Ссылка, ВыборкаДетальныеЗаписи.Наименование);
	КонецЦикла;
	
	Возврат СписокДрайверов;

КонецФункции

&НаСервере
Функция ПроверкаУникальностиПоДрайверу()
	
	Результат = Истина;
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|    1
	|ИЗ
	|    Справочник.ПодключаемоеОборудование КАК ПодключаемоеОборудование
	|ГДЕ
	|    ПодключаемоеОборудование.ДрайверОборудования = &ДрайверОборудования
	|    И ПодключаемоеОборудование.Ссылка <> &Ссылка
	|");
	Запрос.УстановитьПараметр("ДрайверОборудования", Объект.ДрайверОборудования);
	Запрос.УстановитьПараметр("Ссылка" , Объект.Ссылка);
	Результат = Запрос.Выполнить().Пустой();
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура УстановитьДрайвер(Команда)
	
	МенеджерОборудованияКлиент.УстановитьДрайверИзМакета(Объект.ДрайверОборудования);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Элементы.УстановитьДрайвер.Видимость = НЕ ПустаяСтрока(Объект.Ссылка);
	
КонецПроцедуры

#КонецОбласти